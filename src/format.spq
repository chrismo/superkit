// cannot get above EB with uint64 - sorry "ZB" & "YB" and beyond...
const sk_bytes_units=["B", "KB", "MB", "GB", "TB", "PB", "EB"]
const sk_bytes_divisor=1024

func _sk_bytes_unit_index(value): (
  uint64(floor(log(value) / log(sk_bytes_divisor)))
)

func _sk_format_nonzero_bytes(value): (
  f"{uint64(value / pow(sk_bytes_divisor, _sk_bytes_unit_index(value)))} {sk_bytes_units[_sk_bytes_unit_index(value)]}"
)

// deprecated: won't stick around long, forwards to renamed one
func kformat_bytes(value): (
  sk_format_bytes(value)
)

func skdoc_format_bytes(): (
  cast(
    {name:"sk_format_bytes",
     type:"func",
     desc:"Returns the size in bytes in human readable format.",
     args:[{name:"value",desc:"Must be castable to uint64"}],
     examples:[{i:"sk_format_bytes(1048576)",o:"'1 MB'"}] }, <skdoc>)
)

func sk_format_bytes(value): (
  (value == 0) ? "0 B" : _sk_format_nonzero_bytes(value)
)

func skdoc_pad_right(): (
  cast(
    {name:"sk_pad_right",
     type:"func",
     desc:"Inserts pad_char to the right of the string until it reaches target_length.",
     args:[{name:"s",desc:"The string to pad"},
           {name:"pad_char",desc:"The character to pad with"},
           {name:"target_length",desc:"The target length of the string"}],
     examples:[{i:"sk_pad_right('abc', ' ', 5)",o:"'abc  '"}] }, <skdoc>)
)

func sk_pad_right(s, pad_char, target_length): (
  len(s) < target_length ? sk_pad_right(f"{s}{pad_char}", pad_char, target_length) : s
)
